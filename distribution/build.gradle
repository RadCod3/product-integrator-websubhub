/*
* Copyright (c) 2025, WSO2 LLC. (http://www.wso2.com)
*
* WSO2 LLC. licenses this file to you under the Apache License,
* Version 2.0 (the "License"); you may not use this file except
* in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied. See the License for the
* specific language governing permissions and limitations
* under the License.
*/

plugins {
    id 'distribution'
    id 'maven-publish'
}

distributions {
    hub {
        distributionBaseName = 'wso2websubhub'
        def hubDir = project(':websubhub').projectDir
        def scriptsDir = file('scripts/websubhub')
        def confDir = file('conf/websubhub')
        contents {
            from("${hubDir}/target/bin") {
                include "*.jar"
                into "lib"
            }
            
            from("${confDir}") {    
                include "Config.toml"
                into "conf"
            }

            from("${hubDir}/resources") {
                into "conf/resources"
            }
            
            from(scriptsDir) {
                include "*.sh", "*.bat"
                into "bin"
                fileMode = 0755
            }
            
            from(rootProject.projectDir) {
                include "README.md"
                include "LICENSE*"
                into ""
            }
        }
    }

    consolidator {
        distributionBaseName = 'wso2websubhub-consolidator'
        def consolidatorDir = project(':websubhub-consolidator').projectDir
        def scriptsDir = file('scripts/websubhub-consolidator')
        def confDir = file('conf/websubhub-consolidator')
        contents {
            from("${consolidatorDir}/target/bin") {
                include "*.jar"
                into "lib"
            }
            
            from("${confDir}") {
                include "Config.toml"
                into "conf"
            }

            from("${consolidatorDir}/resources") {
                into "conf/resources"
            }

            from(scriptsDir) {
                include "*.sh", "*.bat"
                into "bin"
                fileMode = 0755
            }
            
            from(rootProject.projectDir) {
                include "README.md"
                include "LICENSE*"
                into ""
            }
        }
    }

    websubhubDistribution {
        distributionBaseName = 'wso2websubhub-distribution'
        contents {
            from(zipTree(hubDistZip.archiveFile)) {
                into ""
            }

            from(zipTree(consolidatorDistZip.archiveFile)) {
                into ""
            }

            from(rootProject.projectDir) {
                include "README.md"
                include "LICENSE*"
                into ""
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.property('group')
            artifactId = project.property('name')
            artifact(tasks.getByName("websubhubDistributionDistZip"))
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/wso2/product-integrator-websubhub")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

hubDistZip.dependsOn(':websubhub:build')
hubDistTar.enabled = false

consolidatorDistZip.dependsOn(':websubhub-consolidator:build')
consolidatorDistTar.enabled = false

websubhubDistributionDistZip.dependsOn(hubDistZip)
websubhubDistributionDistZip.dependsOn(consolidatorDistZip)
websubhubDistributionDistTar.enabled = false

build.dependsOn(hubDistZip, consolidatorDistZip, websubhubDistributionDistZip)
